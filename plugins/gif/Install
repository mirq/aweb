; $VER: Install_awebgif_awebsupport
; Description: Installer script for the "awebgif" plugin for AWeb-II 3.0+

(set #welcome
   (cat "This installation procedure will install the \"awebgif\" plugin module for AWeb-II version 3.0 or higher.\n"
        "\n"
        "This plugin enables you to view GIF images in AWeb without the use of datatypes. The plugin has the following features:\n"
        "Optional progressive display\n"
        "Optional playback of animated GIF\n"
        "Transparency\n"
        "Support for image scaling\n"
        "Support for CyberGraphics\n"
   )
)

(set #no-aweb3
   (cat "The \"AWeb3:\" assign was not found.\n\n"
        "This plugin can only be used with AWeb-II version 3.0 or higher."
   )
)

(set #show-docs
   (cat "All necessary files have been copied. "
      "To use the plugin, you have to do some additional setup. "
      "This is described in the documentation. "
      "From the documentation, you can run the setup script that performs the additional setup automatically. "
      "\n"
      "Do you want to view the documentation now?\n"
   )
)

; ---------------------------------------------------------------------------

(complete 0)

(message #welcome)

(if (not (exists "AWeb3:"))
   (abort #no-aweb3)
)

(complete 10)

(if (not (exists "AWeb3:awebsupport"))
   (makedir "AWeb3:awebsupport"
      (infos)
   )
)

(set srcdir (pathonly @icon))

(copyfiles
   (source (tackon srcdir "awebgif.awebplugin"))
   (dest "AWeb3:awebsupport")
   (infos)
)

(complete 50)

(if (not (exists "AWeb3:awebsupport/Docs"))
   (makedir "AWeb3:awebsupport/Docs"
      (infos)
   )
)

(copyfiles
   (source srcdir)
   (choices "awebgif.html" "awebgif.awebrx")
   (dest "AWeb3:awebsupport/Docs")
   (infos)
)

(complete 90)

(if
   (askbool
      (prompt #show-docs)
      (help #show-docs)
      (default 1)
   )
   (
      (run "run AWeb3:AWeb-II file:///AWeb3:awebsupport/Docs/awebgif.html")
   )
)

(set @default-dest (expandpath "AWeb3:awebsupport"))

(complete 100)
