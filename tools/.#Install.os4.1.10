; $VER: Install_AWeb_APL (15 March 2003)
; Description: Installer script for AWeb

(set cpu (database "cpu"))
(set cpu020 (and (<> cpu "68000") (<> cpu "68010")))
(set v51 (>= (/ (getversion "exec.library" (resident)) 65536) 51))

(set awn (getversion "AWeb"))
(set awv (/ awn 65536))
(set awr (- awn (* awv 65536)))

; all strings used in this installer script
(set #need51 "This program requires OS 4.0 or later to function.")
(set #needcpu "This archive was designed for 68020 processor, please download the correct version for your CPU.")
(set #nohelp "No help available.")
(set #welcome
   (cat "\n\nAWeb APL Lite is a fast, multithreaded WWW browser for AmigaOS and MorphOS.\n\n"
        "AWeb APL Lite runs with a wide variety of TCP-stacks, including Genesis and Miami and Roadshow\n\n"
      ("\n\nThis installation procedure will install the AWeb APL Lite browser version %ld.%ld on your system.\n" awv awr)
   )
)

(set #classact
   (cat "IMPORTANT\n\n"
        "This program needs the ClassAct GUI package or ReAction to run.\n"
        "The ClassAct package is not included in the AWeb_APL archive, but is freely available "
        "from Aminet.\n\n"
        "The ReAction package is included as part of the AmigaOS in version 3.5 and above."
   )
)

(set #destdir-prompt "Please select the directory where you want AWeb APL Lite installed.\n(If you do not accept the default, a directory 'AWebPPC' will be created there.)")
(set #destdir-help "A directory for AWebPPC will be created here. By accepting the default directory, most of the files don't have to be copied.")
(set #installing "Installing ")
(set #languages-prompt "Please select which language catalog(s) you want to install")

(set #developer-prompt
    (cat "Do you wish to install the AWeb.developer program"
          "\nalongside the standard AWeb program?"
    )
)
(set #developer-help
    (cat "AWeb.developer is a special build of AWeb with certain extra functions"
         "\nbuilt in. Its primary use is in the investigating and reporting"
         "\nof bugs. There is no extra browsing functionality."
    )
)
(set #developer-no-install "Don't Install AWeb.developer" )
(set #developer-install "Install AWeb.developer")

(set #fixfonts-prompt "The FixFonts utility will be run to make the newly added fonts available.")
(set #fixfonts-help
   (cat "The FixFonts utility is needed to make the newly added fonts available to the system.\n\n"
      "If you intend to use the fonts from this archive, you are strongly encouraged to "
      "run FixFonts now.\n\n"
      "Note that running this program may take a while."
   )
)
(set #charsets-prompt "Please select which charsets you wish to install")

(set #readme "Displaying the version notes readme...")
(set #makeasgn "Making AWeb_APL assignment to S:User-Startup")
(set #bye
   (cat "In case you need help configuring or using AWeb APL Lite, pressing the HELP key "
        "when AWeb is active will bring up the documentation.\n\n\n"
        "Please check the AwebPlugin  drawer included in this archive for setting up internal image decoders. The default settings now utilise these but you may wish to tweak the options"
   )
)

(set #start-prompt "Would you like to start AWeb once the installation is finished?")
(set #start-no "Don't Start AWeb")
(set #start-yes "Start AWeb")

(if (or (= @language "français") (= @language "french")) (
    (set #need51 "Ce programme nécessite AmigaOS 4.0 ou supérieur pour fonctionner.")
    (set #nohelp "Aucune aide disponible.")
    (set #welcome
       (cat "\n\nAWeb APL Lite est un navigateur WEB rapide et multithread pour AmigaOS et MorphOS.\n\n"
            "AWeb APL Lite fonctionne avec une grande variété de pile TCP/IP, incluant Genesis, Miami et Roadshow.\n\n"
          ("\n\nCe script d'installation va installer AWeb APL Lite browser version %ld.%ld sur votre système.\n" awv awr)
       )
    )

    (set #classact
       (cat "IMPORTANT\n\n"
            "Ce programme requiert le package ClassAct ou ReAction pour fonctionner.\n"
            "Le package ClassAct n'est pas inclus dans l'archive AWeb_APL archive, mais est disponible "
            "gratuitement sur Aminet.\n\n"
            "Le package ReAction est inclus dans l'AmigaOS depuis la version 3.5 du système d'exploitation."
       )
    )

    (set #destdir-prompt "Veuillez sélectionner le tiroir où vous voulez installer APL Lite.\n(Si vous n'acceptez pas l'emplacement par défaut, un tiroir 'AWebPPC' sera créé.)")
    (set #destdir-help "Un tiroir pour AWebPPC sera créé ici. En acceptant l'emplacement par défaut, la plupart des fichiers n'auront pas à être copiés.")
    (set #installing "Installation de ")
    (set #languages-prompt "Veuillez sélectionner quel(s) langages vous désirez installer")

    (set #developer-prompt
        (cat "Désirez-vous installer le programme AWeb.developer"
              "\nen plus du programme AWeb standard ?"
        )
    )
    (set #developer-help
        (cat "AWeb.developer est une version spéciale d'AWeb avec des fonctions spéciales supplémentaires."
             "\nSon usage principal est pour la recherche et le rapport de bugs. Il n'y a aucune "
             "\nfonction de navigation supplémentaire."
        )
    )
    (set #developer-no-install "Ne pas installer AWeb.developer" )
    (set #developer-install "Installer AWeb.developer")

    (set #charsets-prompt "Veuillez sélectionner le jeu de caractères que vous désirez installer")

    (set #readme "Affichage du fichier de note de la version...")
    (set #makeasgn "Ajout de l'assignation AWeb_APL dans S:User-Startup")
    (set #bye
       (cat "Si vous avez besoin d'aide pour configurer ou utiliser AWeb APL Lite, pressez la touche HELP "
            "lorsque AWeb est actif pour accéder à la documentation.\n\n\n"
            "Veuillez vérifier le tiroir AwebPlugin inclus dans cette archive pour configurer les décodeurs d'image internes. Les paramètres par défaut sont utilisés, mais vous pourriez vouloir modifier les options."
       )
    )

    (set #start-prompt "Voulez-vous démarrer AWeb une fois l'installation terminée ?")
    (set #start-no "Ne pas lancer AWeb")
    (set #start-yes "Lancer AWeb")
))

; ---------------------------------------------------------------------------

(complete 0)

(if (not v51)
   (abort #need51)
)

(message #welcome)

(run "run >NIL: SYS:Utilities/MultiView Docs/AWeb.gif" (prompt #welcome))

(set @source-dir (pathonly @icon))

(set defdir
   (askdir
      (prompt #destdir-prompt)
      (help #destdir-help)
      (default @source-dir)
   )
)
(set @default-dest defdir)

(set nocopy (= @source-dir @default-dest))

(message #classact)

(set #developer
   (askbool
      (prompt #developer-prompt)
      (help #developer-help)
      (choices #developer-no-install #developer-install )
      (default 1)
  )
)


(if (not nocopy)
    (
    (set @default-dest (tackon @default-dest "AWebPPC"))

       (set update-aweb (exists @default-dest))

    (if (not update-aweb)
        (makedir @default-dest
            (prompt #creating-dir @default-dest)
               (infos)
        )
       )
      (complete 10)

      (copyfiles
         (source "")
         (dest @default-dest)
         (choices "AWeb" "AWebCFG" "AWebJS")
         (prompt #installing "AWeb")
         (infos)
      )
      (if (= #developer 0)
        (copyfiles
             (source "")
              (dest @default-dest)
             (choices "Aweb.developer")
              (prompt #installing "AWeb")
        )
      )
      (complete 20)
    (copyfiles
         (source "")
          (dest @default-dest)
         (choices "AWebPlugin" "Extras" "Extras.info" "AWebPlugin.info")
          (prompt #installing "AWeb")
    )
    (complete 25)
    (copyfiles
         (source "")
          (dest @default-dest)
         (choices "aweblib")
          (prompt #installing "AWeb")
    )
    (copyfiles
         (source "")
          (dest @default-dest)
         (choices "Docs")
          (prompt #installing "AWeb")
    )

    (set #doc-index (tackon @default-dest "Docs/docsearch.index"))
    (if (exists #doc-index)
       (
        (run "C:delete  "  | #doc-index )
       )
    )
      (complete 30)

      (set cache-dir (tackon @default-dest "Cache"))
      (if (not (exists cache-dir))
         (
            (makedir cache-dir
               (prompt #creating-dir cache-dir)
            )
         )
      )
      (complete 40)

    (copyfiles
         (source "")
          (dest @default-dest)
         (choices "apl.txt" "ReadMe" "Images")
          (prompt #installing "AWeb")
          (infos)
    )
      (complete 50)

      (set languages
         (askoptions
            (choices "English" "Bosnian" "Catalan" "Croatian" "Czech" "Danish" "Dutch"  "Finnish"
               "French" "German" "Greek"  "Hungarian" "Italian" "Latvian" "Norwegian"
               "Polish" "Portuguese" "Portuguese-brazil" "Russian" "Serbian" "Slovak" "Slovenian" "Spanish" "Swedish" "Turkish")
            (prompt #languages-prompt)
            (help @askoptions-help)
            (default 1)
         )
      )

      ; If any language calalogs are selected, copy them.

      (if (bitand languages 65534)
         (
            (set catdir (tackon @default-dest "Catalogs"))
            (if (not (exists catdir))
               (makedir catdir
                  (prompt #creating-dir catdir)
               )
            )
            (set bit 2)    ; English implementation, so there is no catalog for it
            (set langnr 1)
            (while (< langnr 25)
               (if (bitand languages bit)
                  (
                     (set lang
                        ; English is never selected from this list , but with new OS4 rules it may be that it should be....
                      (select langnr "English" "Bosnian" "Catalan" "Croatian" "Czech" "Danish" "Dutch"  "Finnish"
                           "French" "German" "Greek" "Hungarian"  "Italian" "Latvian" "Norwegian"
                           "Polish" "Portuguese" "Portuguese-brazil" "Russian" "Serbian" "Slovak" "Slovenian" "Spanish" "Swedish" "Turkish"
                      )
                     )
                     (copyfiles
                        (source (tackon "Catalogs" lang))
                        (choices "aweb.catalog" "awebcfg.catalog")
                        (dest (tackon catdir lang))
                     )
                  )
               )
               (set bit (shiftleft bit 1))
               (set langnr (+ langnr 1))
            )
         )
      )
    )

(complete 60)

; Ask which charsets to install

(set charsets
    (askoptions
        (choices
            "iso-8859-1 windows chars to latin 1"
            "windows-1252 translates to iso-8859-1"
            "iso-8859-2 for polish locales"
            "koi8-r translates koi8-r to windows-1251"
        )
        (prompt #charsets-prompt)
        (help @askoptions-help)
    )
)
(if (bitand charsets 65534)
    (
    (set chardir (tackon @default-dest "Charsets"))
    (if (not (exists chardir))
       (makedir chardir
          (prompt #creating-dir chardir)
       )
    )
    (set bit 1)
    (set charnr 0)
    (while (< charnr 4)
       (if (bitand charsets bit)
          (
             (set charset
                (select charnr "iso-8859-1.charset" "windows-1252.charset" "iso-8859-2.charset" "koi8-r.charset"
                )
             )
             (copyfiles
                (source "Charsets")
                (choices charset)
                (dest chardir)
             )
          )
       )
       (set bit (shiftleft bit 1))
       (set charnr (+ charnr 1))
    )

    )
)
(complete 80)

)



(set #ss_command
   (cat
          "Assign AWEB_APL: "
          '"'
          @default-dest
          '"'
   )
)

(startup "AWEB_APL"

    (prompt #makeasgn)

    (help @startup-help)

    (command

        (#ss_command)
    )
)

(run "run >NIL: " | #ss_command)


(complete 100)

(run "SYS:Utilities/MultiView apl.txt" (prompt #readme))
(run "SYS:Utilities/MultiView ReadMe" (prompt #readme))

(set #aweb_command
    (cat '"' #default-dest 'AWeb"')
)

(set #dontstart
   (askbool
      (prompt #start-prompt)
      (choices #start-no #start-yes)
      (help ".")
      (default 0)
  )
)


(if (= #dontstart 1)

    (run "C:avail flush >NIL:")
    (run "run > NIL: " | #aweb_command)
)

(exit #bye)


